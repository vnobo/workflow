plugins {
    id "org.springframework.boot" version "2.4.1" apply false
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
}

allprojects {

    group = "com.alexbob"
    version = "0.0.1-dev"

    repositories {
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://repo.spring.io/milestone' }
        jcenter()
        mavenCentral()
    }
}

subprojects {

    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = "11"

    [compileJava, compileTestJava]*.options*.encoding = "UTF-8"

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    idea.module {
        outputDir = file("$buildDir/out/")
        testOutputDir = file("$buildDir/test")
    }

    ext {
        set("hutoolVersion", "5.5.+")
        set("weixinVersion", "4.0.+")
        set("hanlpVersion", "portable-1.7.+")
        set("springCloudVersion", "2020.0.+")
        set("springCloudAlibabaVersion", "2.2.+")
        set("springfoxSwagger2Version", "3.0.+")
    }

    dependencies {

        implementation "io.springfox:springfox-boot-starter:${springfoxSwagger2Version}"
        implementation "cn.hutool:hutool-all:${hutoolVersion}"

        implementation "org.springframework.boot:spring-boot-starter-actuator"
        implementation "org.springframework.boot:spring-boot-starter-webflux"
        implementation "org.springframework.boot:spring-boot-starter-amqp"
        implementation "org.springframework.boot:spring-boot-starter-data-redis-reactive"
        implementation "org.springframework.boot:spring-boot-starter-validation"

        implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap'
        implementation "com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config"
        implementation "com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery"
        implementation "org.springframework.cloud:spring-cloud-starter-loadbalancer"
        implementation "com.github.ben-manes.caffeine:caffeine"

        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        annotationProcessor "org.projectlombok:lombok"

        testImplementation("org.springframework.boot:spring-boot-starter-test") {
            exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        }
        testImplementation "io.projectreactor:reactor-test"

    }

    dependencyManagement {
        imports {
            mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }
}


