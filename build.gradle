plugins {
    id "org.springframework.boot" version "2.4.2" apply false
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
}

allprojects {

    group = "com.celesea"
    version = "2021.0.0-dev"

    repositories {
        maven { url "https://artifacts.alfresco.com/nexus/content/repositories/activiti-releases/" }
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://repo.spring.io/milestone" }
        jcenter()
        mavenCentral()
    }
}

subprojects {

    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"


    sourceCompatibility = "11"

    [compileJava, compileTestJava]*.options*.encoding = "UTF-8"

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    idea.module {
        outputDir = file("$buildDir/out/")
        testOutputDir = file("$buildDir/test")
    }

    ext {
        set("springCloudVersion", "2020.0.+")
        set("springCloudAlibabaVersion", "2.2.+")
        set("springfoxSwagger2Version", "3.0.+")
        set("activitiVersion", "7.1.+")
    }

    dependencies {

        implementation "io.springfox:springfox-boot-starter:${springfoxSwagger2Version}"

        implementation "org.springframework.boot:spring-boot-starter-actuator"
        implementation "org.springframework.boot:spring-boot-starter-webflux"
        implementation "org.springframework.boot:spring-boot-starter-validation"

        implementation "org.springframework.cloud:spring-cloud-starter-bootstrap"
        implementation "com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config"
        implementation "com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery"
        implementation "org.springframework.cloud:spring-cloud-starter-loadbalancer"
        implementation "com.github.ben-manes.caffeine:caffeine"

        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        annotationProcessor "org.projectlombok:lombok"

        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "io.projectreactor:reactor-test"
        testImplementation "org.springframework.amqp:spring-rabbit-test"

    }

    dependencyManagement {
        imports {
            mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }

    test {
        useJUnitPlatform()
    }
}